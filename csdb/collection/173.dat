function IndexOfWidestString(const Strings: Classes.TStrings;
  const Font: Graphics.TFont): Integer;
var
  MaxW: Integer;              // records length of longest string to date
  TextW: Integer;             // stores length of each string in list
  Idx: Integer;               // loops through each string in list
  Canvas: Graphics.TCanvas;   // canvas used to measure text
begin
  Assert(Assigned(Font));
  Assert(Assigned(Strings));
  // Exit if string list empty
  Result := -1;
  if Strings.Count = 0 then
    Exit;
  // Create canvas used to measure text
  Canvas := Graphics.TCanvas.Create;
  try
    Canvas.Handle := CreateDisplayDC;
    try
      Canvas.Font := Font;
      // Measure each string's width and record widest in pixels
      MaxW := -MaxInt;  // *will* be updated: even empty string longer than this
      for Idx := 0 to Strings.Count - 1 do
      begin
        TextW := Canvas.TextWidth(Strings[Idx]);
        if TextW > MaxW then
        begin
          Result := Idx;  // executed at least once due to init value of MaxW
          MaxW := TextW;
        end;
      end;
    finally
      // Tidy up
      Windows.DeleteDC(Canvas.Handle);
      Canvas.Handle := 0;
    end;
  finally
    Canvas.Free;
  end;
end;