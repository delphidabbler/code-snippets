function CopyFiles(const SrcDir, DestDir, Wildcard: string): Integer;
var
  Files: Classes.TStringList; // stores files to be copied
  I: Integer;                 // loops thru files in folder
  AFile: string;              // a file to be copied
  SrcPath: string;            // path to SrcDir
  DestPath: string;           // path to DestDir
  Attr: Integer;              // attributes of a file
begin
  Result := 0;
  // Create list to stores files to be copied
  Files := Classes.TStringList.Create;
  try
    // List files per file spec into string list
    if not ListFiles(SrcDir, Wildcard, Files) then
      Exit;
    // Get path of directory containing files
    SrcPath := DirToPath(SrcDir);
    // Loop through all files
    for I := 0 to Pred(Files.Count) do
    begin
      // Get name and attributes of file to be copied
      AFile := SrcPath + Files[I];
      Attr := SysUtils.FileGetAttr(AFile);
      // Copy file if it is not a directory
      if (Attr and SysUtils.faDirectory = 0) then
      begin
        DestPath := DirToPath(DestDir) + Files[I];
        if Windows.CopyFile(PChar(AFile), PChar(DestPath), False) then
          // File copied: count it
          Inc(Result);
      end;
    end;
  finally
    // Tidy up
    Files.Free;
  end;
end;